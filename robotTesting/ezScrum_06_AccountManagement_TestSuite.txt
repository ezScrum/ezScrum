*** Settings ***
Documentation     test ezScrumn account management page
Suite Setup       Test Account Management - PreCondition
Suite Teardown    Test Account Management - PostCondition
Force Tags        AccountManagement
Resource          keywords/AccountManagement/ezScrum_AccountManagement_SetupTeardown.txt
Resource          keywords/AccountManagement/ezScrum_AccountManagement_Management.txt
Resource          keywords/common_resource.txt
Resource          keywords/ezScrum_Login.txt
Resource          keywords/Project/ezScrum_Project.txt
Resource          Global Define.txt
Resource          ServerConfig.txt
Library           BuiltIn
Library           keywords/lib/Selenium2Improved.py

*** Variables ***
${tsAccountId}    TestAccountID
${tsAccountName}    TestAccountName
${tsAccountMail}    TestAccountMail@scrum.tw

*** Test Cases ***
Test Account Management - Add Account
    [Setup]    Test Account Management - Add Account Setup
    Add Account By Parameter    ${tsAccountId}    ${tsAccountName}    ${tsAccountMail}
    Wait Until Page Contains    Success.
    Check Account In Edit    ${tsAccountId}    ${tsAccountName}    ${tsAccountMail}
    # admin Log out
    Click Image    images/logout.gif
    Title Should Be    ezScrum Logon!
    # log in by new account
    Input Text    userId    ${tsAccountId}
    Input Password    password    ${tsAccountId}
    Submit Form    logonForm
    Title Should Be    ezScrum, SSLab NTUT
    [Teardown]    Test Account Management - Add Account Teardown

Test Account Management - Add Duplicate Account
    [Setup]    Test Account Management - Add Duplicate Account Setup
    Add Account By Parameter    ${tsAccountId}    ${tsAccountName}    ${tsAccountMail}
    Wait Until Page Contains    Success.
    Add Account By Parameter    ${tsAccountId}    ${tsAccountName}    ${tsAccountMail}
    Wait Until Page Contains    is already existed
    # click already exist dialog button
    Click Button    xpath=//span[text()="ID: [ ${tsAccountId} ] is already existed."]/../../../../..//button[text()="OK"]
    # Cancel the add account window
    ${accountCancelBtn}=    Find Current Window Element    Add New Account    Cancel
    Element Enable And Submit    ${accountCancelBtn}
    Xpath Should Match X Times    //td/div[text()="${tsAccountId}"]    1
    [Teardown]    Test Account Management - Add Duplicate Account Teardown

Test Account Management - Edit Account
    [Setup]    Test Account Management - Edit Account Setup
    Edit Account    ${tsAccountId}
    Wait Until Page Contains    Success.
    Check Account In Edit    ${tsAccountId}    ${tsAccountId}NameEdited    ${tsAccountId}Edited@scrum.tw
    [Teardown]    Test Account Management - Edit Account Teardown

Test Account Management - Delete Account
    [Setup]    Test Account Management - Delete Account Setup
    Add Account By Parameter    ${tsAccountId}    ${tsAccountName}    ${tsAccountMail}
    Wait Until Page Contains    Success.
    Xpath Should Match X Times    //td/div[text()="${tsAccountId}"]    1
    # delete account
    Delete Account    ${tsAccountId}
    Xpath Should Match X Times    //td/div[text()="${tsAccountId}"]    0
    # log out
    Click Image    images/logout.gif
    Title Should Be    ezScrum Logon!
    # log in by deleted account
    Login With Invalid Credentials Should Fail    ${tsAccountId}    ${tsAccountId}    Invalid user name or password
    [Teardown]    Test Account Management - Delete Account Teardown

Test Account Management - Enable Account
    [Setup]    Test Account Management - Enable Account Setup
    # Disable Account
    Disable Account    ${tsAccountId}
    # logout
    Click Image    images/logout.gif
    Title Should Be    ezScrum Logon!
    # login with disable account
    Login With Invalid Credentials Should Fail    ${tsAccountId}    ${tsAccountId}    Invalid user name or password
    # admin login
    Input Text    userId    admin
    Input Password    password    admin
    Submit Form    logonForm
    Title Should Be    ezScrum, SSLab NTUT
    click element    xpath=//td[text()="Management"]
    Wait Until Page Contains Element    xpath=//div[@id="Management_Top_Panel"]//td[text()="Account Management"]
    Wait Until Page Contains Element    xpath=//div[@id="Account_Management_Grid_Panel"]//td[1]/div[text()="admin"]
    # Enable Account
    Enable Account    ${tsAccountId}
    # logout
    Click Image    images/logout.gif
    Title Should Be    ezScrum Logon!
    # login with enable account
    Input Text    userId    ${tsAccountId}
    Input Password    password    ${tsAccountId}
    Submit Form    logonForm
    Title Should Be    ezScrum, SSLab NTUT
    [Teardown]    Test Account Management - Enable Account Teardown

Test Account Management - Click Add Account Can Display Correct Information
    [Documentation]    當按下編輯的按鈕後，在按新增的按鈕，表單欄位沒有reset
    [Tags]    Bug
    [Setup]    Test Account Management - Click Add Account Can Display Correct Information Setup
    # 選擇一個已存在的帳號並click edit button
    SwitchToPage    Management
    Wait Until Page Contains Element    xpath=//div[@id="Management_Top_Panel"]//td[text()="Account Management"]
    Mouse Down    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-0" and (text()="${tcAccountID}1")]
    Mouse Up    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-0" and (text()="${tcAccountID}1")]
    Click Element    xpath=//button[text()="Edit Information"]
    ${xpathCancelButtonInEditAccount}=    Find Current Window Element    Edit Information    Cancel
    Element Enable And Submit    ${xpathCancelButtonInEditAccount}
    # click add button
    Click Element    xpath=//button[text()="Add Account"]
    # verify Account Info
    Compare Field Content    //span[text()="Add New Account"]/../../../../..//input[@name="account"]    ${EMPTY}    # User ID
    Compare Field Content    //span[text()="Add New Account"]/../../../../..//input[@name="name"]    ${EMPTY}    # User Name
    Compare Field Content    //span[text()="Add New Account"]/../../../../..//input[@name="passwd"]    ${EMPTY}    # Password
    Compare Field Content    //span[text()="Add New Account"]/../../../../..//input[@name="reenter"]    ${EMPTY}    # Re-enter
    Compare Field Content    //span[text()="Add New Account"]/../../../../..//input[@name="mail"]    ${EMPTY}    # E-mail Address
    Xpath Should Match X Times    //span[text()="Add New Account"]/../../../../..//input[@type="checkbox" and @name="enable"]    1    # enabel check box
    ${xpathCancelButtonInAddAccount}=    Find Current Window Element    Add New Account    Cancel
    Element Enable And Submit    ${xpathCancelButtonInAddAccount}
    [Teardown]    Test Account Management - Click Add Account Can Display Correct Information Teardown

Test Account Management - Do Not Delete Account of Admin
    [Documentation]    1. 系統不能delete Admin的帳號
    ...    2. 驗證點選admin帳號時Delete Account Button是disable
    ...    3. 驗證點選User帳號時Delete Account Button是enable
    [Tags]    Bug
    [Setup]    Do Not Delete Account of Admin Setup
    ${admin}=    Set Variable    admin
    Set Selenium Speed    1
    # 驗證點選admin帳號時Delete Account Button是disable
    Mouse Down    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-0" and (text()="${admin}")]
    Mouse Up    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-0" and (text()="${admin}")]
    Xpath Should Match X Times    //table[@id="AccountManagement_deleteAccountBtn" and @class="x-btn${SPACE * 3}x-btn-text-icon${SPACE * 2}x-item-disabled"]    1
    # 驗證點選User帳號時Delete Account Button是enable
    Mouse Down    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-0" and (text()="${tcAccountID}1")]
    Mouse Up    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-0" and (text()="${tcAccountID}1")]
    Xpath Should Match X Times    //table[@id="AccountManagement_deleteAccountBtn" and @class="x-btn${SPACE * 3}x-btn-text-icon${SPACE}"]    1
    [Teardown]    Do Not Delete Account of Admin Teardown

Test Account Management - Change Password
    [Documentation]    驗證改變密碼的功能
    ...    更改密碼後能夠正常登入
    [Tags]    Bug
    [Setup]    Test Account Management - Change Password Setup
    Edit Account    ${tsAccountId}
    Wait Until Page Contains    Success.
    Check Account In Edit    ${tsAccountId}    ${tsAccountId}NameEdited    ${tsAccountId}Edited@scrum.tw
    # logout
    Click Image    images/logout.gif
    Title Should Be    ezScrum Logon!
    # log in by user account
    Input Text    userId    ${tsAccountId}
    Input Password    password    ${tsAccountId}
    Submit Form    logonForm
    Title Should Be    ezScrum, SSLab NTUT
    # logout
    Click Image    images/logout.gif
    Title Should Be    ezScrum Logon!
    # login with admin
    Input Text    userId    admin
    Input Password    password    admin
    Submit Form    logonForm
    Title Should Be    ezScrum, SSLab NTUT
    # change password
    Wait Until Page Contains Element    xpath=//td[text()="Management"]
    Click element    xpath=//td[text()="Management"]
    ${newPassword}=    Set Variable    01234567890123456789
    Change Account Password    ${tsAccountId}    ${newPassword}
    # logout
    Click Image    images/logout.gif
    Title Should Be    ezScrum Logon!
    # log in by user account
    Input Text    userId    ${tsAccountId}
    Input Password    password    ${newPassword}
    Submit Form    logonForm
    Title Should Be    ezScrum, SSLab NTUT
    [Teardown]    Test Account Management - Change Password Teardown

Test Account Management - Validate Confirm Password
    [Documentation]    驗證comfirm password
    ...    1. 只填password或re-enter的欄位，submit為disable
    ...    2. password和re-enter資訊不一致，submit為disable
    ...    3. password和re-enter一致，submit為enble
    [Tags]    Bug
    [Setup]    Test Account Management - Validate Confirm Password Setup
    ${errorPassword}=    Set Variable    errorPassword
    ${correctPassword}=    Set Variable    correctPassword
    Set Selenium Speed    ${SELENIUM_SPEED}
    Mouse Down    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-0" and (text()="${tsAccountId}")]
    Mouse Up    xpath=//div[@class="x-grid3-cell-inner x-grid3-col-0" and (text()="${tsAccountId}")]
    # click "Change Password" Button
    Click Element    xpath=//button[text()="Change Password"]
    # update Account Info
    Focus AND Input Text    //span[text()="Change Password"]/../../../../..//input[@name="passwd"]    ${correctPassword}    # Password
    Xpath Should Match X Times    //span[text()="Change Password"]//../../../../..//button[text()="Save"]/../../../../../../table[@class="x-btn${SPACE * 3}x-btn-noicon${SPACE}x-item-disabled"]    1
    Focus AND Input Text    //span[text()="Change Password"]/../../../../..//input[@name="reenter"]    ${errorPassword}    # Re-enter
    Xpath Should Match X Times    //span[text()="Change Password"]//../../../../..//button[text()="Save"]/../../../../../../table[@class="x-btn${SPACE * 3}x-btn-noicon${SPACE}x-item-disabled"]    1
    Focus AND Input Text    //span[text()="Change Password"]/../../../../..//input[@name="reenter"]    ${correctPassword}    # Re-enter
    Xpath Should Match X Times    //span[text()="Change Password"]//../../../../..//button[text()="Save"]/../../../../../../table[@class="x-btn${SPACE * 3}x-btn-noicon${SPACE}"]    1
    # Save
    ${xpathEditPasswordCancelButton}=    Find Current Window Element    Change Password    Cancel
    Element Enable And Submit    ${xpathEditPasswordCancelButton}
    [Teardown]    Test Account Management - Validate Confirm Password Teardown

Test Account Management - User Edit Information
    [Documentation]    1. 驗證使用者可以正確的更改個人資料
    ...    2. 驗證使用者可以重複修改個人資料
    [Setup]    Test Account Management - User Modify Personal Information Setup
    # 第一次更新個人資料
    Focus AND Input Text    //div[@id="userEditInformationTab"]//input[@name="name"]    ${tsAccountId}NameEdited1    # User Name
    Focus AND Input Text    //div[@id="userEditInformationTab"]//input[@name="mail"]    ${tsAccountId}Edited1@scrum.tw    # E-mail Address
    Wait Until Page Contains Element    xpath=//div[@id="userEditInformationTab"]//button[text()="Save"]
    Click Element    xpath=//div[@id="userEditInformationTab"]//button[text()="Save"]
    Wait Until Page Contains    Success.
    Xpath Should Match X Times    //div[@id="UserNameInfo_Management" and @class="UserProfile" and text()="${tsAccountId}(${tsAccountId}NameEdited1)"]    1
    # 第二次更新個人資料
    Focus AND Input Text    //div[@id="userEditInformationTab"]//input[@name="name"]    ${tsAccountId}NameEdited2    # User Name
    Focus AND Input Text    //div[@id="userEditInformationTab"]//input[@name="mail"]    ${tsAccountId}Edited2@scrum.tw    # E-mail Address
    Wait Until Page Contains Element    xpath=//div[@id="userEditInformationTab"]//button[text()="Save"]
    Click Element    xpath=//div[@id="userEditInformationTab"]//button[text()="Save"]
    Wait Until Page Contains    Success.
    Xpath Should Match X Times    //div[@id="UserNameInfo_Management" and @class="UserProfile" and text()="${tsAccountId}(${tsAccountId}NameEdited2)"]    1
    [Teardown]    Test Account Management - User Modify Personal Information Teardown

Test Account Management - User Change Password
    [Documentation]    1. 驗證使用者可以正確的更改密碼
    ...    2. 驗證使用者可以重複修改個人密碼
    ...    3. 修改完會立即重新登入驗證密碼是否修改成功
    [Setup]    Test Account Management - User Modify Personal Information Setup
    ${errorPassword}=    Set Variable    errorPassword
    ${correctPassword}=    Set Variable    ${tsAccountId}EditPasswd
    # 切換至Change Password Tab Panel
    Click Element    xpath=//span[text()="Change Password"]/../../..
    # 驗證save button狀態
    # 只輸入password欄位
    Focus AND Input Text    //div[@id="userChangePasswordTab"]//input[@name="passwd"]    ${correctPassword}    # Password
    Xpath Should Match X Times    //div[@id="userChangePasswordTab"]//button[text()="Save"]/../../../../../..//table[@class="x-btn${SPACE * 3}x-btn-noicon${SPACE}x-item-disabled"]    1
    # 輸入re-enter錯誤密碼
    Focus AND Input Text    //div[@id="userChangePasswordTab"]//input[@name="reenter"]    ${errorPassword}    # Password
    Xpath Should Match X Times    //div[@id="userChangePasswordTab"]//button[text()="Save"]/../../../../../..//table[@class="x-btn${SPACE * 3}x-btn-noicon${SPACE}x-item-disabled"]    1
    # 輸入re-enter正確密碼
    Focus AND Input Text    //div[@id="userChangePasswordTab"]//input[@name="reenter"]    ${correctPassword}    # Password
    Xpath Should Match X Times    //div[@id="userChangePasswordTab"]//button[text()="Save"]/../../../../../..//table[@class="x-btn${SPACE * 3}x-btn-noicon${SPACE}"]    1
    # 送出欲修改的密碼
    Click Element    xpath=//div[@id="userChangePasswordTab"]//button[text()="Save"]/..
    Wait Until Page Contains    Success.
    # 登出系統
    Logon ezScrum System
    # 用更新的密碼登入系統
    Login Page With Account In The Same Browser    ${tsAccountId}    ${correctPassword}
    [Teardown]    Test Account Management - User Modify Personal Information Teardown

Test Account Management - User Change Password And Edit Information
    [Documentation]    1. 修改個人密碼和個人資訊
    ...    2. 修改完會立即重新登入驗證密碼是否修改成功
    ...    3. 驗證專案列表右上角個人名稱是否正確。
    [Setup]    Test Account Management - User Modify Personal Information Setup
    ${correctPassword}=    Set Variable    ${tsAccountId}EditPasswd
    # 切換至Change Password Tab Panel
    Click Element    xpath=//span[text()="Change Password"]/../../..
    # 送出欲修改的密碼
    Focus AND Input Text    //div[@id="userChangePasswordTab"]//input[@name="passwd"]    ${correctPassword}    # Password
    Focus AND Input Text    //div[@id="userChangePasswordTab"]//input[@name="reenter"]    ${correctPassword}    # Password
    Xpath Should Match X Times    //div[@id="userChangePasswordTab"]//button[text()="Save"]/../../../../../..//table[@class="x-btn${SPACE * 3}x-btn-noicon${SPACE}"]    1
    Click Element    xpath=//div[@id="userChangePasswordTab"]//button[text()="Save"]/..
    Wait Until Page Contains    Success.
    # 切換至Edit Information Tab Panel
    Click Element    xpath=//span[text()="Edit Information"]/../../..
    Focus AND Input Text    //div[@id="userEditInformationTab"]//input[@name="name"]    ${tsAccountId}NameEdited    # User Name
    Focus AND Input Text    //div[@id="userEditInformationTab"]//input[@name="mail"]    ${tsAccountId}Edited@scrum.tw    # E-mail Address
    Click Element    xpath=//div[@id="userEditInformationTab"]//button[text()="Save"]
    Wait Until Page Contains    Success.
    Xpath Should Match X Times    //div[@id="UserNameInfo_Management" and @class="UserProfile" and text()="${tsAccountId}(${tsAccountId}NameEdited)"]    1
    # 登出系統
    Logon ezScrum System
    # 用更新的密碼登入系統
    Login Page With Account In The Same Browser    ${tsAccountId}    ${correctPassword}
    Xpath Should Match X Times    //div[@id="UserNameInfo_Project" and @class="UserProfile" and text()="${tsAccountId}(${tsAccountId}NameEdited)"]    1
    [Teardown]    Test Account Management - User Modify Personal Information Teardown

Test Account Management - Admin's email shouldn't be null
    # admin login
    Input Text    userId    admin
    Input Password    password    admin
    Submit Form    logonForm
    Title Should Be    ezScrum, SSLab NTUT
    Click element    xpath=//td[text()="Management"]
    Wait Until Page Contains Element    xpath=//div[@id="Management_Top_Panel"]//td[text()="Account Management"]
    Xpath Should Match X Times    //div[@id="Account_Management_Grid_Panel"]/div[2]/div[2]/div/div/div[2]/div/div/table/tbody/tr/td[3]//div[text()="example@ezScrum.tw"]    1
